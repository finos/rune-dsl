module com.regnosys.rosetta.GenerateRosetta

import com.regnosys.rosetta.xcore.compressor.ParserCompressorFragment
import com.regnosys.rosetta.xcore.extractor.ParserCaseExtractorFragment
import com.regnosys.rosetta.xcore.generator.serializer.RosettaSerializerFragment

import org.eclipse.xtext.xtext.generator.XtextGenerator
import org.eclipse.xtext.xtext.generator.StandardLanguage
import org.eclipse.xtext.xtext.generator.parser.antlr.XtextAntlrGeneratorFragment2
import org.eclipse.xtext.xtext.generator.model.project.StandardProjectConfig
import org.eclipse.xtext.xtext.generator.scoping.ImportNamespacesScopingFragment2

var parentDir = ".."

Workflow {
	component = XtextGenerator {
	    standaloneSetup = {
            scanClasspath = true
        }
		configuration = {
			project = StandardProjectConfig {
				baseName = "rune"
				rootPath = parentDir
				mavenLayout = true
				createEclipseMetaData = false
				runtime = {
				    enabled = true
				    name = "rune-lang"
				    root = "${parentDir}/rune-lang"
				    src = "${parentDir}/rune-lang/src/main/java"
				    srcGen = "${parentDir}/rune-lang/src-gen/main/java"
				}
				runtimeTest = {
					enabled = false
				}
				genericIde = {
                    enabled = true
                    name = "rune-ide"
                    root = "${parentDir}/rune-ide"
                    src = "${parentDir}/rune-ide/src/main/java"
                    srcGen = "${parentDir}/rune-ide/src-gen/main/java"
                }
				eclipsePlugin = {
					enabled = false
				}
				eclipsePluginTest = {
					enabled = false
				}
				web = {
                    enabled = false
				}
			}
			code = {
				encoding = "UTF-8"
				lineDelimiter = "\n"
				fileHeader = "/*\n * Copyright (c) REGnosys 2018 (www.regnosys.com) \n * generated by Xtext \${version}\n */"
				preferXtendStubs = false
			}
		}
		language = StandardLanguage {
			referencedResource = "platform:/resource/rune-lang/model/Rosetta.xcore"
			referencedResource = "platform:/resource/rune-lang/model/RosettaSimple.xcore"
			referencedResource = "platform:/resource/rune-lang/model/RosettaExpression.xcore"
			name = "com.regnosys.rosetta.Rosetta"
			fileExtensions = "rosetta"

			parserGenerator = XtextAntlrGeneratorFragment2 {
				// set to true to get a debug Grammar
				debugGrammar = true
			}
			scopeProvider = ImportNamespacesScopingFragment2 {}
			serializer = RosettaSerializerFragment {
				generateStub = false
			}
			validator = {
				// Generates checks for @Deprecated grammar annotations, an IssueProvider and a corresponding PropertyPage
				generateDeprecationValidation = true
				composedCheck = "org.eclipse.xtext.validation.NamesAreUniqueValidator"
				composedCheck = "com.regnosys.rosetta.validation.RosettaValidator"
			}
			generator = {
				generateXtendStub = true
			}
			junitSupport = {
				junitVersion = "5"
			}
			formatter = {
				generateStub = true
				generateXtendStub = true
			}
			fragment = ParserCompressorFragment {
				// Enabled to produce shorter Java code and ease debugging at the expense of a little uglier (because more dense) code
				joinLinesEagerly = true
				grammarFile = "${parentDir}/rune-ide/src-gen/main/java/com/regnosys/rosetta/ide/contentassist/antlr/internal/InternalRosettaParser.java"
				grammarFile = "${parentDir}/rune-lang/src-gen/main/java/com/regnosys/rosetta/parser/antlr/internal/InternalRosettaParser.java"
			}
			fragment = ParserCaseExtractorFragment {
				grammarFile = "${parentDir}/rune-ide/src-gen/main/java/com/regnosys/rosetta/ide/contentassist/antlr/internal/InternalRosettaParser.java"
				grammarFile = "${parentDir}/rune-lang/src-gen/main/java/com/regnosys/rosetta/parser/antlr/internal/InternalRosettaParser.java"
			}
		}
	}
}
