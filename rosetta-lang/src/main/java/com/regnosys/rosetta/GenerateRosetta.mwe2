module com.regnosys.rosetta.GenerateRosetta

import org.eclipse.xtext.xtext.generator.XtextGenerator
import org.eclipse.xtext.xtext.generator.StandardLanguage
import org.eclipse.xtext.xtext.generator.parser.antlr.XtextAntlrGeneratorFragment2
import org.eclipse.xtext.xtext.generator.model.project.StandardProjectConfig

var parentDir = ".."

Workflow {
	component = XtextGenerator {
	    standaloneSetup = {
            scanClasspath = true
        }
		configuration = {
			project = StandardProjectConfig {
				baseName = "rosetta"
				rootPath = parentDir
				mavenLayout = true
				createEclipseMetaData = false
				runtime = {
				    enabled = true
				    name = "rosetta-lang"
				    root = "${parentDir}/rosetta-lang"
				    src = "${parentDir}/rosetta-lang/src/main/java"
				    srcGen = "${parentDir}/rosetta-lang/src-gen/main/java"
				}
				runtimeTest = {
					enabled = true
					name = "rosetta-testing"
					root = "${parentDir}/rosetta-testing"
					src = "${parentDir}/rosetta-testing/src/test/java"
					srcGen = "${parentDir}/rosetta-testing/src-gen/main/java"
				}
				genericIde = {
                    enabled = true
                    name = "rosetta-ide"
                    root = "${parentDir}/rosetta-ide"
                    src = "${parentDir}/rosetta-ide/src/main/java"
                    srcGen = "${parentDir}/rosetta-ide/src-gen/main/java"
                }
				eclipsePlugin = {
					enabled = false
				}
				eclipsePluginTest = {
					enabled = false
				}
				web = {
                    enabled = false
				}
			}
			code = {
				encoding = "UTF-8"
				lineDelimiter = "\n"
				fileHeader = "/*\n * Copyright (c) REGnosys 2018 (www.regnosys.com) \n * generated by Xtext \${version}\n */"
				preferXtendStubs = true
			}
		}
		language = StandardLanguage {
			referencedResource = "platform:/resource/com.regnosys.rosetta/model/Rosetta.xcore"
			referencedResource = "platform:/resource/com.regnosys.rosetta/model/RosettaSimple.xcore"
			referencedResource = "platform:/resource/com.regnosys.rosetta/model/RosettaExpression.xcore"
			referencedResource = "platform:/resource/com.regnosys.rosetta/model/RosettaTranslate.xcore"
			name = "com.regnosys.rosetta.Rosetta"
			fileExtensions = "rosetta"

			parserGenerator = XtextAntlrGeneratorFragment2 {
				// set to true to get a debug Grammar
				debugGrammar = true
			}
			serializer = {
				generateStub = false
			}
			validator = {
				// composedCheck = "org.eclipse.xtext.validation.NamesAreUniqueValidator"
				// Generates checks for @Deprecated grammar annotations, an IssueProvider and a corresponding PropertyPage
				generateDeprecationValidation = true
				composedCheck = "com.regnosys.rosetta.validation.RosettaValidator"
			}
			generator = {
				generateXtendStub = true
			}
			junitSupport = {
				junitVersion = "5"
			}
			formatter = {
				generateStub = true
			}
		}
	}
}
